plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.145'
}

version = project.modVersion
group = "yalter.mousetweaks"

base {
    archivesName = "MouseTweaks-neoforge-mc${project.neoMcVersion}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(project.modJavaVersion)

sourceSets {
    main {
        java {
            exclude 'yalter/mousetweaks/fabric'
            exclude 'yalter/mousetweaks/ModMenuApiImpl.java'
            exclude 'yalter/mousetweaks/forge'
        }

        resources {
            exclude 'fabric.mod.json'
            exclude 'META-INF/mods.toml'
        }
    }
}

processResources {
    inputs.property "version", project.modVersion
    filesMatching("META-INF/neoforge.mods.toml") {
        expand([
                "version": project.modVersion,
                "neoLoaderVersion": project.neoLoaderVersion,
                "modCredits": project.modCredits,
                "modDescription": project.modDescription,
        ])
    }
}

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', "mousetweaks"
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', "mousetweaks"
        programArgument '--nogui'
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${project.neoVersion}"
}

task srcJar(type: Jar) {
    archiveClassifier.set("src")
    from sourceSets.main.allJava
}

task apiJar(type: Jar) {
    archiveClassifier.set("api")
    from(sourceSets.main.output) {
        include 'yalter/mousetweaks/api/**'
    }
}

artifacts {
    archives srcJar
    archives apiJar
}
